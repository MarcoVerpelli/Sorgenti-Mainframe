program MandelbrotSet(output); (* poster dello insieme di Mandelbrot *)
const maxIt=27; (* numero di "colori" in realtÃ  caratteri *)
var str:array[1..maxIt] of char; (* tavolozza dei colori/caratteri *)

   procedure Mandelbrot(minRE,minIM,maxRE,maxIM:real; strp:integer);
   label 99; (* eat your heart out Dijkstra *)
   var stepX,stepY,re,im,zr,zi,a,b:real;
       Xsize,Ysize,x,y,n,s:integer;
   begin
      Xsize:=131*strp; (* le colonne vengono contate 0..131 *)
      Ysize:=48*strp; (* 49 righe approssimano il rapporto 3:2 *)
      stepX:=(maxRE-minRE)/Xsize;
      stepY:=(maxIM-minIM)/Ysize;
      for s:=1 to strp (* ciclo principale: quante striscie *)
      do begin
            for y:=0 to Ysize (* ciclo righe *)
            do begin
                  im:=minIM+stepY*y;
                  write(' ');
                  for x:=132*(s-1) to 132*s-1 (* ciclo colonne *)
                  do begin
                        re:=minRE+stepX*x;
                        zr:=re;
                        zi:=im;
                        for n:=1 to maxIt (* ciclo colori/caratteri *)
                        do begin
                              a:=zr*zr;
                              b:=zi*zi;
                              if(a+b)>4
                              then goto 99;
                              zi:=zr*zi*2+im;
                              zr:=a-b+re
                           end;
   99:                  write(str[n]:1)
                     end;
                  writeln
               end;
            if s<strp (* finito una stricia, se ne seguono altre... *)
            then writeln('1') (* ...inizia in una nuova pagina *)
         end
   end;

begin
   str:='ABCDEFGHIJKLMNOPQRSTUVWXYZ '; (* i colori/caratteri *)
   (* stampa un rettangolo 3:2 "spalmato" su 5 stricie *)
   Mandelbrot(-2,-1,1,1,5)
end.
